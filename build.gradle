allprojects  {
  apply plugin: 'maven'

  group = 'com.wakaleo.gameoflife'
version = '1.0-SNAPSHOT'
}

subprojects {
  apply plugin: 'java'
  apply plugin: 'jacoco'
  apply plugin: 'findbugs'
  apply plugin: 'pmd'

  sourceCompatibility = 1.7
  targetCompatibility = 1.7

  
  repositories {
    mavenLocal()
    jcenter()
  }

  test {
    include '**/When*.class'
    include '**/*Test.class'
    exclude '**/integration/**/*'
  }

  task intTest(type: Test, dependsOn: testClasses) {
    include '**/integration/**/*'
    reports.html.destination "$buildDir/reports/intTest"
    ignoreFailures = true
  }

  task jacocoIntTestReport(type: JacocoReport) {
    executionData = files("$buildDir/jacoco/intTest.exec")
    sourceDirectories = files(sourceSets.main.java.srcDirs)
    classDirectories = files(sourceSets.main.output.classesDir)

    reports.html.destination "$buildDir/reports/jacoco/intTest"
  }

  findbugsTest.enabled = false
  pmdTest.enabled = false
  pmd.ruleSetFiles = files("$rootDir/config/pmd/pmd-rules.xml")
  pmd.toolVersion = '4.2.5'
  check.dependsOn(intTest)
  
  configurations.all {
}
  dependencies {
    testCompile group: 'junit', name: 'junit', version:'4.11'
    testCompile group: 'org.hamcrest', name: 'hamcrest-all', version:'1.1'
    testCompile group: 'org.mockito', name: 'mockito-all', version:'1.9.0'
}

  
}
